plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.savet.local.ocr'
    compileSdk 33

    defaultConfig {
        applicationId "com.savet.local.ocr"
        minSdk 24
        targetSdk 33
        versionCode gitCommitNum()
        versionName "1." + releaseTime()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    sourceSets {
        main {
            assets {
                srcDirs 'src/main/assets'
            }
        }
    }
}

dependencies {
    // include jar/aar
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    // constraint layout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    // material UI
    implementation 'com.google.android.material:material:1.8.0'
    // livedata
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    // navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.6.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.6.0'
    // preference fragment
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // sketch library(image)
    def sketch_version = '3.3.0'
    // 提供了 Sketch 的核心功能以及单例和依赖此单例实现的一些便捷的扩展函数，
    // implementation "io.github.panpf.sketch3:sketch:${sketch_version}"

    // zoom-image library
    def zoom_version = '1.0.2'
    // 提供缩放、平移、定位、旋转、超大图子采样功能
    implementation "io.github.panpf.zoomimage:zoomimage-view-glide:${zoom_version}"

    // flexbox library
    // 提供流式布局
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // module base-library
    implementation project(path: ':baselibrary')
    // module networkd
    implementation project(':network')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

/**
 * 自定义功能区
 */

// 自定义任务-打印编译相关信息
tasks.register('getBuildConfig') {
    println "time: ${releaseTime()}\ncommit time: ${gitCommitNum()}"
}

/**
 * 获取格式化时间, 如24.050916
 * @return 格式化(yy.MMddHH)的时间字符串
 */
static String releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

/**
 * 获取git提交次数
 * @return 提交的次数, 如果有异常则抛出并返回0
 */
static Integer gitCommitNum() {
    try {
        return Integer.parseInt('git rev-list HEAD --count'.execute().text.trim())
    } catch (e) {
        e.printStackTrace()
        println 'warning : pls use git repository!'
    }
    return 0
}